types dabtypes

   enum Etat
      automaton-state

   class Carte
      id       string[4]        "Identifiant de la carte"
      code     string[4]        "Identifiant de la carte"
      month    byte             "Jour de péremption de la carte"
      year     ushort           "Année de péremption de la carte"
      nbEssais byte             "Nombre d'essais en échec de la carte"
      
   class Compte
      id       string[4]        "Identifiant du compte"
      solde    double           "Solde du compte"
      autorise boolean          "Le compte est-il autorisé ?"
      
   class Information            "Les informations relatives à une transaction"
      carte  dabtypes.Carte     "La carte utilisée pour la transaction"
      compte dabtypes.Compte    "Le compte associé à la carte"

   class EtatDuDab
      etat        dabtypes.Etat "Etat courant de l'automate du composant Controleur"
      soldeCaisse double        "Montant de la caisse en €"

   generation
      C    as 'DBT'            in "dabtypes-c/src-gen"
      C++  as 'hpms::dabtypes' in "dabtypes-cpp/src-gen"
      Java as 'hpms.dabtypes'  in "dabtypes-java/src-gen"

interface IHM
   event ejecterLaCarte
   event ejecterLesBillets
      montant double            "Montant à donner à l'utilisateur du DAB"
   event confisquerLaCarte
   event placerLesBilletsDansLaCorbeille
   event shutdown

interface UniteDeTraitement
   data etatDuDab               "Partage des données décrivant l'état du DAB"
      dabtypes.EtatDuDab        
   event maintenance            
      maintenance boolean       "Maintenance active (true) ou inactive (false)"
   event rechargerLaCaisse      
      montant double            "Montant à rajouter dans la caisse"
   event anomalie               
      anomalie boolean          "Anomalie active (true) ou inactive (false)"
   event carteInseree           
      carteID string[4]         "Identifiant de la carte"
   event codeSaisi              
      code string[4]            "Code de la carte"
   event montantSaisi           
      montant double            "Montant du retrait en €"
   event carteRetiree
   event billetsRetires
   event annulationDemandeeParLeClient
   event shutdown

interface SiteCentral
   request informations         "Requête asynchrone auprès du site central qui retourne les informations de carte et de compte à partir de l'id de carte"
      response                  
         dabtypes.Information   "Les informations de carte et de compte nécessaires"
      from                      
         carteID string[4]      "Identifiant de la carte"
   event incrNbEssais           
      carteID string[4]         "Identifiant de la carte"
   event retrait                
      carteID string[4]         "Identifiant de la carte"
      montant double            "Montant du retrait en €"
   event shutdown

component Distributeur 
   offers   IHM
   requires UniteDeTraitement
   generation
      C    as 'DAB'       in "distributeur-c/src-gen"
      C++  as 'hpms::dab' in "distributeur-cpp/src-gen"
      Java as 'hpms.dab'  in "distributeur-java/src-gen"

component Controleur
   offers   UniteDeTraitement
   requires IHM SiteCentral
   timeout
      saisieDuCode      30 seconds
      saisieDuMontant   30 seconds
      retraitDeLaCarte  10 seconds
      retraitDesBillets 10 seconds
   uses an automaton 
   generation
      C    as 'UDT'       in "udt-c/src-gen"
      C++  as 'hpms::udt' in "udt-cpp/src-gen"
      Java as 'hpms.udt'  in "udt-java/src-gen"

component Banque
   offers SiteCentral
   generation
      C    as 'SC'       in "sc-c/src-gen"
      C++  as 'hpms::sc' in "sc-cpp/src-gen"
      Java as 'hpms.sc'  in "sc-java/src-gen"

deployment isolated
   process sc 127.0.0.1:2416
      instance sc of Banque
   process udt1 127.0.0.1:2417
      instance udt1 of Controleur
         requires
            SiteCentral sc
            IHM         ihm1
   process ihm1 127.0.0.1:2418
      instance ihm1 of Distributeur
         requires
            UniteDeTraitement udt1
   process udt2 127.0.0.1:2419
      instance udt2 of Controleur
         requires
            SiteCentral sc
            IHM         ihm2
   process ihm2 127.0.0.1:2420
      instance ihm2 of Distributeur
         requires
            UniteDeTraitement udt2

deployment mixed
   process sc 127.0.0.1:2416
      instance sc of Banque
   process dab 127.0.0.1:2417
      instance udt1 of Controleur
         requires
            SiteCentral sc
            IHM         ihm1
      instance ihm1 of Distributeur
         requires
            UniteDeTraitement udt1
      instance udt2 of Controleur
         requires
            SiteCentral sc
            IHM         ihm2
      instance ihm2 of Distributeur
         requires
            UniteDeTraitement udt2
 