types dabtypes

   enum Etat
      automaton-state

   class Carte
      id       string[4]        "Identifiant de la carte"
      code     string[4]        "Identifiant de la carte"
      month    byte             "Jour de péremption de la carte"
      year     ushort           "Année de péremption de la carte"
      nbEssais byte             "Nombre d'essais en échec de la carte"
      
   class Compte
      id       string[4]        "Identifiant du compte"
      solde    double           "Solde du compte"
      autorise boolean          "Le compte est-il autorisé ?"
      
   class Information            "Les informations relatives à une transaction"
      carte  dabtypes.Carte     "La carte utilisée pour la transaction"
      compte dabtypes.Compte    "Le compte associé à la carte"

   class EtatDuDab
      etat        dabtypes.Etat "Etat courant de l'automate du composant Controleur"
      soldeCaisse double        "Montant de la caisse en €"

interface IHM
   event ejecterLaCarte
   event ejecterLesBillets
      montant double            "Montant à donner à l'utilisateur du DAB"
   event confisquerLaCarte
   event placerLesBilletsDansLaCorbeille
   event arret

interface UniteDeTraitement
   data etatDuDab               "Partage des données décrivant l'état du DAB"
      dabtypes.EtatDuDab        
   event rechargerLaCaisse      
      montant double            "Montant à rajouter dans la caisse"
   event anomalie               
      anomalie boolean          "Anomalie active (true) ou inactive (false)"
   event carteInseree           
      carteID string[4]         "Identifiant de la carte"
   event codeSaisi              
      code string[4]            "Code de la carte"
   event montantSaisi           
      montant double            "Montant du retrait en €"
   event carteRetiree
   event billetsRetires
   event annulationDemandeeParLeClient
   event arret

interface Maintenable
   event maintenance
      maintenance boolean "Maintenance active (true) ou inactive (false)"

interface SiteCentral
   request informations         "Requête asynchrone auprès du site central qui retourne les informations de carte et de compte à partir de l'id de carte"
      response                  
         dabtypes.Information   "Les informations de carte et de compte nécessaires"
      from                      
         carteID string[4]      "Identifiant de la carte"
   event incrNbEssais           
      carteID string[4]         "Identifiant de la carte"
   event retrait                
      carteID string[4]         "Identifiant de la carte"
      montant double            "Montant du retrait en €"
   event arret

component Distributeur
   offers   IHM
   requires Maintenable UniteDeTraitement

component Controleur
   offers   Maintenable UniteDeTraitement
   requires IHM SiteCentral
   timeout
      saisieDuCode      30 seconds
      saisieDuMontant   30 seconds
      retraitDeLaCarte  10 seconds
      retraitDesBillets 10 seconds
   uses an automaton

component Banque
   offers SiteCentral
   requires Maintenable

deployment isolated
   process sc
      instance sc of Banque
         requires
            Maintenable from { udt1, udt2 }
   process udt1
      instance udt1 of Controleur
         requires
            IHM         from ihm1
            SiteCentral from sc
   process ihm1
      instance ihm1 of Distributeur
         requires
            Maintenable from udt1
            UniteDeTraitement from udt1
   process udt2
      instance udt2 of Controleur
         requires
            IHM         from ihm2
            SiteCentral from sc
   process ihm2
      instance ihm2 of Distributeur
         requires
            Maintenable from udt2
            UniteDeTraitement from udt2

deployment mixed
   process sc
      instance sc of Banque
         requires
            Maintenable from { udt1, udt2 }
   process dab1
      instance udt1 of Controleur
         requires
            IHM         from ihm2
            SiteCentral from sc
      instance ihm1 of Distributeur
         requires
            Maintenable from udt2
            UniteDeTraitement from udt2
   process dab2
      instance udt2 of Controleur
         requires
            IHM         from ihm1
            SiteCentral from sc
      instance ihm2 of Distributeur
         requires
            Maintenable from udt1
            UniteDeTraitement from udt1

deployment allin
   process one
      instance sc of Banque
         requires
            Maintenable from { udt1, udt2 }
      instance udt1 of Controleur
         requires
            IHM         from ihm1
            SiteCentral from sc
      instance ihm1 of Distributeur
         requires
            Maintenable from udt1
            UniteDeTraitement from udt1
      instance udt2 of Controleur
         requires
            IHM         from ihm2
            SiteCentral from sc
      instance ihm2 of Distributeur
         requires
            Maintenable from udt2
            UniteDeTraitement from udt2
